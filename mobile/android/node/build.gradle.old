plugins {
    id 'com.android.library'
    id 'kotlin-android'
}
android {
    compileSdk 31
    defaultConfig {
        minSdk 21
        targetSdk 28
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    lintOptions {
        abortOnError false
    }
}
repositories {
    flatDir {
        dirs '../build'
    }
}
dependencies {
    implementation(name: 'astral', ext: 'aar')
    implementation project(':mobile:android:mod')
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.1'
    implementation 'androidx.core:core-ktx:1.8.0'
    testImplementation 'junit:junit:4.13.2'
}

task buildGo(type: Exec) {
    group 'build'
    commandLine 'go', 'build', '-o', '../build/astral', './'
}

task buildGoMobile(type: Exec) {
    group 'build'
    dependsOn buildGo
    inputs.files file('../build/astral')
    outputs.upToDateWhen {
        file('../build/astral.aar').exists()
    }
    workingDir '../'
    commandLine './buildGo.sh'
}

preBuild.dependsOn buildGoMobile
